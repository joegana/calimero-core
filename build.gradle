buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.3.1'
ext.desc = 'Calimero, a free KNX network library'

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin:'application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'

group = 'com.github.calimero'
version = '2.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options',
	'-Werror'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]


mainClassName = 'tuwien.auto.calimero.Settings'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['test']
        java.exclude 'resources/', '**/.gradle'
        resources.srcDirs = ['test/resources']
    }
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
    from("${projectDir}") {
        include 'LICENSE*'
        into "META-INF"
    }
}

task testSourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'testSources'
    from sourceSets.test.allSource
    from("${projectDir}") {
        include 'LICENSE*'
        into "META-INF"
    }
}

artifacts {
    archives testJar
    archives testSourcesJar
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

test {
	useJUnitPlatform() {
		excludeTags 'ft12', 'slow'
//		excludeTags 'knxnetip'
	}
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact testJar {
                classifier "tests"
            }
            artifact testSourcesJar {
                classifier "test-sources"
            }
        }
    }
}

dependencies {
    api 'org.slf4j:slf4j-api:1.8.0-beta2'
    api 'org.usb4java:usb4java-javax:1.3.0'
	compileOnly 'javax.xml.stream:stax-api:1.0-2'

	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
	testCompileOnly 'junit:junit:4.12'
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
}
